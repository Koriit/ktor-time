/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "com.github.ben-manes.versions" version "0.41.0"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    mavenCentral()
}

ext {
    ktorVersion = "1.6.7"
    jacksonVersion = "2.13.1"
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // KTOR
    implementation "io.ktor:ktor-server-core:$ktorVersion"

    // Jackson
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    // TESTING
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+((-r)|(release)|(final)|(ga))?$/
    return !(version.toLowerCase() ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

targetCompatibility = 1.8
sourceCompatibility = targetCompatibility

def compilerArgs = [
        "-Xjsr305=strict"
]

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.43.2"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.7"
}

if (!hasProperty("fast")) test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java

            pom {
                name = "Ktor Time Converters"
                description = "Ktor data converters for JSR310 Java Time types"
                url = "https://github.com/Koriit/ktor-time"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/ktor-time/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = 'koriit'
                        name = 'Aleksander Stelmaczonek'
                        email = 'al.stelmaczonek@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Koriit/ktor-time.git'
                    developerConnection = 'scm:git:ssh://github.com/Koriit/ktor-time.git'
                    url = 'https://github.com/Koriit/ktor-time'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = findProperty("sonatype.user") ?: System.getenv("SONATYPE_USERNAME")
                password = findProperty("sonatype.password")?: System.getenv("SONATYPE_PASSWORD")
            }

            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = findProperty("signing.key") ?: System.getenv("PGP_SIGNING_KEY")
    def signingPassword = findProperty("signing.password") ?: System.getenv("PGP_SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
